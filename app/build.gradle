apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'

apply from: '../gradle/scripts/jacoco.gradle'

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

android {
    compileSdkVersion buildConfig.compileSdk
    defaultConfig {
        applicationId "com.michaelcarrano.detectivedroid"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode 16
        versionName "2.1.8"

        resConfig "en"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildFeatures {
            compose = false
            buildConfig = true
            viewBinding = true
            dataBinding = false
        }
    }

    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file("release/detectivedroid-release.keystore")
            storePassword System.getenv("SIGNING_CONFIG_RELEASE_KEYSTORE_PWD")
            keyAlias "detective_droid"
            keyPassword System.getenv("SIGNING_CONFIG_RELEASE_KEY_PWD")
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    coreLibraryDesugaring libs.desugar

    implementation libs.bundles.kotlin

    implementation libs.coreKtx
    implementation libs.constraintLayout
    implementation libs.preference
    implementation libs.recyclerview
    implementation libs.bundles.lifecycle
    implementation libs.bundles.navigation

    implementation libs.material
    implementation libs.openSourceLicenses

    implementation libs.roxie

    implementation libs.bundles.dagger
    kapt libs.dagger.compiler
    kapt libs.dagger.processor

    implementation libs.bundles.rx

    implementation libs.timber

    debugImplementation libs.bundles.facebook

    debugImplementation libs.leakCanary
    implementation libs.plumber

    testImplementation libs.bundles.unitTests
    androidTestImplementation libs.bundles.androidTests
}
