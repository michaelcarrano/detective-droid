import com.michaelcarrano.detectivedroid.buildsrc.Libs

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'

apply from: '../gradle/scripts/jacoco.gradle'

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

android {
    compileSdkVersion buildConfig.compileSdk
    defaultConfig {
        applicationId "com.michaelcarrano.detectivedroid"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode 4
        versionName "2.0.0"

        resConfig "en"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildFeatures {
            compose = false
            buildConfig = true
            viewBinding = true
            dataBinding = false
        }
    }

    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    signingConfigs {
        release {
            storeFile rootProject.file("release/detectivedroid-release.keystore")
            storePassword System.getenv("SIGNING_CONFIG_RELEASE_KEYSTORE_PWD")
            keyAlias "detective_droid"
            keyPassword System.getenv("SIGNING_CONFIG_RELEASE_KEY_PWD")
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    coreLibraryDesugaring Libs.desugar

    implementation Libs.Kotlin.stdlib
    implementation Libs.Kotlin.reflect

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.constraintlayout
    implementation Libs.AndroidX.preference
    implementation Libs.AndroidX.recyclerview
    implementation Libs.AndroidX.Lifecycle.extensions
    implementation Libs.AndroidX.Navigation.fragment
    implementation Libs.AndroidX.Navigation.ui

    implementation Libs.Google.material
    implementation Libs.Google.openSourceLicensesLibrary

    implementation Libs.roxie

    implementation Libs.Dagger.dagger
    kapt Libs.Dagger.compiler

    implementation Libs.Dagger.androidSupport
    kapt Libs.Dagger.androidProcessor

    implementation Libs.RxJava.rxJava
    implementation Libs.RxJava.rxKotlin
    implementation Libs.RxJava.rxAndroid

    implementation Libs.timber

    debugImplementation Libs.Facebook.flipper
    debugImplementation Libs.Facebook.soloader

    debugImplementation Libs.leakCanary

    testImplementation Libs.json
    testImplementation Libs.junit
    testImplementation Libs.mockitoKtx
    testImplementation Libs.AndroidX.Test.archCoreTesting
    testImplementation Libs.AndroidX.Test.core
    testImplementation Libs.AndroidX.Test.runner
    testImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.espressoCore
    androidTestImplementation Libs.AndroidX.Test.junit
}
