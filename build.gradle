buildscript {
    ext.buildConfig = [
            'compileSdk': 31,
            'minSdk'    : 21,
            'targetSdk' : 31,
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath libs.androidGradlePlugin

        classpath libs.kotlinGradlePlugin

        classpath libs.openSourceLicensesGradlePlugin

        classpath libs.navigationSafeArgsGradlePlugin

        classpath libs.jacoco
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.17.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(libs.versions.ktlint.get())
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}
